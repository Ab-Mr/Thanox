import com.google.common.io.Files

apply plugin: 'com.android.application'
apply plugin: 'github.tornaco.rhino.plugin.stub_gen'

apply from: 'thanos_properties.gradle'


android {
    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId rootProject.ext.thanosApplicationId
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode rootProject.ext.versionCode as int
        versionName rootProject.ext.versionName
        println(versionCode)
        println(versionName)
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            Properties localProp = new Properties()
            File propFile = new File("${project.rootProject.rootDir.path}/local.properties")
            if (propFile.exists()) {
                //noinspection UnstableApiUsage
                localProp.load(Files.asByteSource(propFile).openStream())
            }

            storeFile file("${project.rootProject.rootDir.path}/environment/keys/android.jks")

            storePassword localProp.containsKey("KEYSTORE_PASS") ? localProp.getProperty("KEYSTORE_PASS") : project.hasProperty("KEYSTORE_PASS") ? KEYSTORE_PASS : System.getenv("KEYSTORE_PASS")
            keyAlias localProp.containsKey("ALIAS_NAME") ? localProp.getProperty("ALIAS_NAME") : (project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME"))
            keyPassword localProp.containsKey("ALIAS_PASS") ? localProp.getProperty("ALIAS_PASS") : project.hasProperty("ALIAS_PASS") ? ALIAS_PASS : System.getenv("ALIAS_PASS")

            println("keyAlias: ${keyAlias}")
        }
    }

    buildTypes {
        release {
            minifyEnabled Boolean.parseBoolean(System.getProperty("minifyEnabled", Boolean.TRUE.toString()))
            shrinkResources Boolean.parseBoolean(System.getProperty("shrinkResources", Boolean.TRUE.toString()))
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled Boolean.parseBoolean(System.getProperty("minifyEnabled", Boolean.TRUE.toString()))
            shrinkResources Boolean.parseBoolean(System.getProperty("shrinkResources", Boolean.TRUE.toString()))
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "market"
    productFlavors {
        row {
            dimension "market"
            versionNameSuffix "-row"
        }

        prc {
            dimension "market"
            versionNameSuffix "-prc"
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    buildFeatures {
        dataBinding = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def flavor = variant.name
            def versionName = variant.versionName
            outputFileName = "thanox_${versionName}.apk"
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/androidx.room_room-runtime.version'
        // https://github.com/vsch/flexmark-java
        exclude 'META-INF/LICENSE-LGPL-2.1.txt'
        exclude 'META-INF/LICENSE-LGPL-3.txt'
        exclude 'META-INF/LICENSE-W3C-TEST'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/base.kotlin_module'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support Libraries
    implementation deps.support.app_compat
    implementation deps.support.material
    implementation deps.support.recyclerview
    implementation deps.support.preference
    implementation deps.support.constraint

    implementation deps.logging.firecrasher

    // Chart
    implementation deps.chart

    // Licensesdialog
    implementation deps.licensesdialog

    // Arch
    annotationProcessor deps.lifecycle.compiler

    // Glide
    annotationProcessor deps.glide.compiler

    // Xposed
    compileOnly deps.xposed.api

    compileOnly deps.lombok.core
    annotationProcessor deps.lombok.core

    compileOnly project(':annotation_processors:permission-requester-annotation')
    annotationProcessor project(':annotation_processors:permission-requester-compiler')

    implementation deps.retrofit.retrofit2
    implementation deps.retrofit.converter_gson
    implementation deps.retrofit.adapter_rxjava2

    // Modules
    implementation deps.thanos_internal.module_donate

    implementation project(':modules:module_common')
    implementation project(':modules:module_ops')
    implementation project(':modules:module_easteregg')
    implementation project(':modules:module_activity_trampoline')
    implementation project(':modules:module_component_manager')
    implementation project(':modules:module_exam')
    implementation project(':modules:module_notification_recorder')
    implementation project(':modules:module_locker')
    implementation project(':modules:module_filepicker')
    implementation project(':modules:module_profile')

    implementation project(':android_framework:base')
    implementation project(':android_framework:res')
    implementation project(':android_framework:patch-magisk:patch-framework')

    implementation deps.thanos_internal.services
    implementation deps.thanos_internal.patchx_entry

    implementation project(':nitro:nitro_framework')

    implementation project(':third_party:apkbuilder')
    implementation project(':third_party:badge')
    implementation project(':third_party:time-duration-picker')
    implementation project(':third_party:dateformatter')
    implementation project(':third_party:recyclerview-fastscroll')
    implementation project(':third_party:search')
}

buildDir = rootProject.projectDir.path + "/out/app/"